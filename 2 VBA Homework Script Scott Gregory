Attribute VB_Name = "Module1"
Sub stock_looping()

' Establish that this macro will loop through each worksheet
For Each ws In Worksheets
ws.Activate

' Set variable for tiker name
Dim ticker As String

' Set variable for total volume for per ticker
Dim ticker_total As Double
ticker_total = 0

' Set variable to count rows within each ticker (in order to ID first and last row per ticker)
Dim ticker_row_count As Double
ticker_row_count = 0

' Set variable to hold difference between opening stock price at beginning and end of year for each ticker
Dim annual_change As Double

' Set variable to hold % difference of above
Dim percent_change As Double

' Set variable to for column number
Dim column As Integer
column = 1

' Set variable to for row number
Dim row As Long

' Set variable to hold value for last row of content
Dim lastrow As Long

' Set variable for opening price for first day of the year per ticker
Dim jan_price As Double

' Set variable for closing price for last day of the year per ticker
Dim dec_price As Double

' Set variable for summary table row number
Dim summarytablerow As Integer
summarytablerow = 2

' Establish headers for summary table
ws.Cells(1, 9).Value = "Ticker"
ws.Cells(1, 10).Value = "Yearly Change"
ws.Cells(1, 11).Value = "Percent Change"
ws.Cells(1, 12).Value = "Total Stock Volume"

' Find last row of data for main chart
lastrow = ws.Cells(Rows.Count, 1).End(xlUp).row

    ' Loop through all rows in the column
    For row = 2 To lastrow
        
        ' Start ticker row counter
        ticker_row_count = ticker_row_count + 1
            
            ' Check to see if the next row has the same ticker, if not...
            If ws.Cells(row + 1, column).Value <> ws.Cells(row, column).Value Then
            
                ' Grab name of ticker
                ticker = ws.Cells(row, column).Value
                
                ' Print the ticker name to the summary table
                ws.Cells(summarytablerow, 9).Value = ticker
                
                ' Add the values in the volume column
                ticker_total = ticker_total + ws.Cells(row, 7).Value
                
                        ' Check to be sure ticker total isn't 0
                        If ticker_total > 0 Then
                
                            ' Print the ticker total to the summary table
                            ws.Cells(summarytablerow, 12).Value = ticker_total
                        
                        Else
                            ' Otherwise print N/A to the summary row
                            ws.Cells(summarytablerow, 12).Value = "N/A"
                        
                        End If
                
                ' Identify last opening price for each ticker
                dec_price = ws.Cells(row, 6).Value
                
                ' Identify the first opening price for each ticker(subtract one less than ticker row count from last(current) row #)
                jan_price = ws.Cells((row - (ticker_row_count - 1)), 3).Value
                
                        ' Check to be sure first opening price is greater than zero
                        If jan_price > 0 Then
                        
                            ' Calculate annual change in opening price from Jan to  Dec
                            annual_change = dec_price - jan_price
                            
                            ' Print the change from Jan to Dec to the summary table
                            ws.Cells(summarytablerow, 10).Value = annual_change
                
                             ' Calculate percent change in opening price from Jan to  Dec
                            percent_change = (annual_change / jan_price)
                
                            ' Print the % change to the summary table (and format as percent)
                            ws.Cells(summarytablerow, 11).Value = percent_change
                            ws.Cells(summarytablerow, 11).NumberFormat = "0.00%"
                        
                        Else
                            ' Otherwise print N/A to the summary row for change and % change
                            ws.Cells(summarytablerow, 10).Value = "N/A"
                            ws.Cells(summarytablerow, 11).Value = "N/A"
                        
                        End If
                    
                ' Move to the next row of the summary table
                summarytablerow = summarytablerow + 1
                
                ' Reset the ticker total
                ticker_total = 0
                
                'Reset the ticker row counter
                ticker_row_count = 0
            
            ' If the next row is for the same ticker...
            Else
                ticker_total = ticker_total + ws.Cells(row, 7).Value
            
            End If
         
    Next row
    
    
    
    ' Conditional Format the summary table to color code annual change
    
    ' Find last row of data for summary chart
    lastrowsummary = ws.Cells(Rows.Count, 9).End(xlUp).row
    
    ' Loop through all rows in the summary table
    For row = 2 To lastrowsummary
    
        ' Conditional to identify if cell is N/A
        If ws.Cells(row, 10).Value = "N/A" Then
            ' If so, do not color
            ws.Cells(row, 10).Interior.ColorIndex = 0
        
        ' If the number is negative, color red
        ElseIf ws.Cells(row, 10).Value < 0 Then
            ws.Cells(row, 10).Interior.ColorIndex = 3
            
        ' If the number is positive, color green
        ElseIf ws.Cells(row, 10).Value > 0 Then
            ws.Cells(row, 10).Interior.ColorIndex = 4
            
        End If
    
    Next row
    
Next ws

End Sub
